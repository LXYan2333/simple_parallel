
@PACKAGE_INIT@

if(CMAKE_VERSION VERSION_LESS "3.25.0")
   message(FATAL_ERROR "CMake >= 3.25.0 required")
endif()

if(CMAKE_Fortran_COMPILER_LOADED AND NOT "@SIMPLE_PARALLEL_Fortran_BINDING@")
   message(FATAL_ERROR "simple_parallel is not compiled with fortran support, thus should not be used in a Fortran project. Please recompile simple_parallel with CMake option SIMPLE_PARALLEL_Fortran_BINDING set to ON")
endif()

include(CMakeFindDependencyMacro)

find_dependency(Boost REQUIRED COMPONENTS mpi headers thread container)
set(MPI_CXX_SKIP_MPICXX ON)
find_dependency(MPI REQUIRED COMPONENTS C CXX)
find_dependency(Microsoft.GSL REQUIRED)
find_dependency(HWY REQUIRED)

if("@SIMPLE_PARALLEL_Fortran_BINDING@")
   find_dependency(MPI REQUIRED COMPONENTS Fortran)
   find_dependency(re2 REQUIRED)
   find_dependency(PkgConfig REQUIRED)
   pkg_check_modules(libelf REQUIRED IMPORTED_TARGET libelf)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/simple_parallelTargets.cmake")
